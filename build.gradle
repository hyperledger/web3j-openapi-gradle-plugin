plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'java'
    id 'java-gradle-plugin'
    id 'idea'
    id 'jacoco'
    id "com.diffplug.gradle.spotless" version "3.25.0"
    id 'io.codearte.nexus-staging' version '0.21.1'
    id "de.marcphilipp.nexus-publish" version "0.4.0"
    id 'com.gradle.plugin-publish' version '0.10.1'
    id "de.undercouch.download" version "4.0.0"
    id "com.jfrog.bintray" version "1.8.4"
}

description = 'Gradle plugin providing tasks to generate Web3j-OpenAPI project from Solidity.'
sourceCompatibility = 1.8

// FIXME: change to correct versions
ext {
    web3jVersion = '4.6.1'
    web3jOpenAPIVersion = '4.6.001-SNAPSHOT'
    junitVersion = '5.5.2'
    gradlePluginVersion = '4.5.11'
}

apply {
    [
        'bintray',
        'jacoco',
        'java',
        'javadoc',
        'repositories',
        'publish',
        'spotless',
    ].each { buildScript ->
        download {
            src "https://raw.githubusercontent.com/web3j/build-tools/master/gradle/$buildScript/build.gradle"
            dest "$rootDir/gradle/$buildScript/build.gradle"
            overwrite true
            quiet true
            onlyIfModified true
        }
        from("$rootDir/gradle/$buildScript/build.gradle")
    }
}

repositories {
    maven {
        url 'https://repo.gradle.org/gradle/libs-releases-local/'
    }
    maven {
        url 'https://plugins.gradle.org/m2/'
    }
    maven {
        url 'https://dl.bintray.com/hotkeytlt/maven'
    }
    mavenLocal()
}

dependencies {
    implementation "org.web3j:web3j-gradle-plugin:$gradlePluginVersion"
    implementation ("org.web3j:abi:$web3jVersion") {
        exclude group: 'org.slf4j', module: 'slf4j-nop'
    }
    implementation "org.web3j.openapi:web3j-openapi-codegen:$web3jOpenAPIVersion"
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
}

gradlePlugin {
    plugins {
        web3j {
            id = 'org.web3j.openapi'
            implementationClass = 'org.web3j.openapi.gradle.plugin.OpenApiPlugin'
        }
    }
}

//pluginBundle {
//    website = 'https://web3j.io/'
//    vcsUrl = 'https://github.com/web3j/web3j-openapi-gradle-plugin'
//    description = 'Gradle plugin providing tasks to generate Web3j-OpenAPI project from Solidity.'
//    tags = [
//        'solidity',
//        'ethereum',
//        'web3.js',
//        'web3j',
//        'openapi'
//    ]
//    plugins {
//        openapi {
//            // id is captured from java-gradle-plugin configuration
//            displayName = project.description
//        }
//    }
//}

def generatedResources = "$buildDir/generated-resources/main"

sourceSets {
    main {
        output.dir(generatedResources, builtBy: 'generateVersionProperties')
    }
}

task generateVersionProperties {
    doLast {
        new File(generatedResources).mkdirs()
        def generated = new File(generatedResources, "version.properties")
        generated.text = "version=$rootProject.version"
    }
}
tasks.withType(Test) {
    reports.html.destination file("${reporting.baseDir}/${name}")
    useJUnitPlatform()
}
